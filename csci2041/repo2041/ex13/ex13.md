# Exercise Set 13: Modules and Functors

*CSci 2041: Advanced Programming Principles, Spring 2019 (Hopper)*

**Due:** Monday, April 29 at 11:59pm

## 1. Modules and Interfaces

The `ex13` directory contains three set implementations:
* `listSet.ml`
* `hashSet.ml`
* `funSet.ml`

and three interface files:
* `sset.mli`
* `rset.mli`
* `fset.mli`

For each implementation/interface pair below explain whether the pair are compatible, and if they are not, why?

1. `listSet.ml : sset.mli`

2. `hashSet.ml : sset.mli`

3. `funSet.ml : sset.mli`

4. `listSet.ml : rset.mli`

5. `hashSet.ml : rset.mli`

6. `funSet.ml : rset.mli`

7. `listSet.ml : fset.mli`

8. `hashSet.ml : fset.mli`

9. `funSet.ml : fset.mli`

### Answer format
Recored your answers in a markdown list in the file `module-interface.md` as follows:
```
1. `listSet.ml : sset.mli`
> Legal
>> listSet provides all types and values in the sset signature
```
For full credit, you should correctly answer at least 7/9 cases.

## 2.  Structs, Signatures, Sharing, and Functors

For each of the following OCaml expressions, state whether it is a legal program or not, and explain why:

1. ```ocaml
    module A : sig
        val x : string
    end = struct
        let x = 7
    end
    ```

2. ```ocaml
    module B : sig
      val b : string
      val b : string
    end = struct
      let b = "b"
    end
    ```

3. ```ocaml
    module c = struct let c=1 end
    ```

4. ```ocaml
    module D : sig
      val f : int -> int
      val g : int list -> int list
    end = struct
      let g x = x
      let f x = g x
    end
    ```

5. ```ocaml
   let module E = struct let e=1 end in E.e
   ```

6. ```ocaml
    let module F : sig
      type t
      val f : t -> t
    end = struct
      type t = int
      let f x = x+1
    end in F.f
    ```

7. ```ocaml
    module type GaSig = sig type t end
    module type GbSig = sig val x : int end
    module Ga : GaSig with type t = int = struct
      type t = int
    end
    module Gb : GbSig = struct
      let x = 1
    end
    module Gc : sig
      include GaSig
      val x : t
    end = struct
      include Ga
      include Gb
    end
    ```

8. ```ocaml
    module type HSig = sig
      type 'a t
      val f : 'a t -> unit
    end
    let module H : HSig with type 'a t = 'a list = struct
      let rec f = function [] -> ()
      | h::t -> f t
    end in H.f [1;2]
    ```

### Answer format

Record your answers in the file `modules.md` in a markdown list using the format
```
1. `module A`
> Not Legal
>> The value `x` has mismatched types in the signature and struct
```

### Functor Types and Values

For each of the following programs, what is the type and value of the final declaration in the program? If the type is opaque, specify the value as `<abstr>`.

1. ```ocaml
    module A(X : sig val x : string end ) = struct let x = X.x ^ X.x end
    module AA = A(A(struct let x = "rain " end))
    let a = AA.x ^ "go away!"
    ```

2. ```ocaml
    module type BSig = sig
      type t
      val n : t
      val cat : t -> string -> t
    end
    module B(X : BSig) : BSig = struct
      type t = X.t
      let n = X.cat X.n "b"
      let cat = X.cat
    end
    module BBB = B(B(B(struct type t = string let n = "" let cat = (^) end)))
    let b = BBB.n
    ```

3. ```ocaml
    module type CSig = sig
      type t
      val n : t
      val cat : t -> string -> t
    end
    module C(X : CSig) : CSig with type t = X.t = struct
      type t = X.t
      let n = X.cat X.n "c"
      let cat = X.cat
    end
    module CCC = C(C(C(struct type t = string let n = "" let cat = (^) end)))
    let c = CCC.n
    ```

4. ```ocaml
    module type DSig = sig
      type t
      val d : t
    end
    module D1(X : DSig) = struct
      type t = X.t
      let d = X.d
    end
    module D2(X : DSig) : DSig with type t = X.t = struct
      type t = X.t
      let d = X.d
    end
    let d = let module D3 = D1(struct type t = int list let d = [1] end) in
    let module D4 = D2(struct type t = int list let d = [1] end) in
    D3.d @ D4.d
    ```

5. ```ocaml
    module type ESig = sig
      type t = E1 of int | E2
      val e : t
    end
    module E(M : ESig) = struct
      type t = M.t
      let e = M.E1 0
    end
    module EE = struct type t = E1 of int | E2 let e = E2 end
    let e = let module EEE = E(EE) in EEE.e
    ```

### Answer format
Record your answers in a markdown list in the file `functor-values.md` Your solution file should consist of a markdown ordered list, where each list item has a line of the form `type: t` followed by a line of the form `value: v`, e.g.

```
1. type: string
value: "nofreebies"
```


To get full credit, your solution must be correct for at least 9/13 of the programs.

## 3. Module code

In this section, we'll develop some simple modules and functors, working in the file `functors.ml`  

### Stack module

In `functors.ml`, add a module `Stack` that defines a type `'a t = 'a list`; the functions `push : 'a -> 'a t -> 'a t`, `pop : 'a t -> 'a t`, and `top : 'a t -> 'a`; and the value `empty : 'a t`.   (`push` conses a new top onto the stack, `pop` returns the rest or `invalid_arg "pop"` on an empty stack; `top` returns the head or `invalid_arg "top"` on an empty stack; `empty` is `[]`).  There will be one test case for each item in the signature, and test evaluations for `Stack.pop (Stack.push "a" Stack.empty) = Stack.empty`, which should evaluate to `true`) and `Stack.top (Stack.push 17 Stack.empty)`, which should evaluate to `17`.


### Counting Modules

Next, add definitions for:
+ a module type `N` that requires a value `n` of type `int` (Test case: the correct signature is defined)
+ a functor `Next` that acts on a module `X` satisfying `n`, also satisfies `N`, and sets its `n` value to `X.n+1` (Test case: `let module N1 = Next(struct let n = 0 end) in N1.n` evaluates to `1`)
+ a module `N3` that applies `Next` to an anonymous struct with `n=2`. (Test case: `N3.n` evaluates to `3`)

### `Nat` arithmetic

#### `Nat` module

Define a module `Nat` that defines the type `t = Zero | Succ of t`, and provides functions `to_int : Nat.t -> int`, `of_int : int -> Nat.t`, (the bodies of these functions can be found in the quizzes and lectures on induction), the value `zero : Nat.t`, and functions `incr : Nat.t -> Nat.t`, and `decr : Nat.t -> Nat.t` (Calling `Nat.decr Zero` should raise `Invalid_argument "decr"`, but this won't be a test case).  Test cases:

+ `let x = Nat.zero in ((Nat.decr (Nat.incr x)) = x)` evaluates to `true`
+ `Nat.to_int (Nat.incr Nat.zero)` evaluates to `1`
+ `Nat.to_int (Nat.of_int 3)` evaluates to `3`.  

#### `NatSig` signature

Define a module signature `NatSig` that requires a type `t`, a value `zero : t` and the functions `incr : t -> t` and `decr : t -> t`.  Test cases will check for each of these belonging to the signature.

#### `NatMath` functor

Define a functor `NatMath` that acts on a module `M` that satisfies `NatSig` to provide functions `plus : M.t -> M.t -> M.t` and `times : M.t -> M.t -> M.t`.  (You can recover these from `plus_nat` and `mul_nat` in the induction lecture).  Define the module `NatOps` to be the application of the `NatMath` functor to the `Nat` module.  Four test cases: `Nat.to_int (NatOps.plus (Nat.of_int 2) (Nat.of_int 3))` should evaluate to `5`; `Nat.to_int (NatOps.times Nat.zero Nat.zero)` should evaluate to `0`; `Nat.to_int (NatOps.times (Nat.of_int 2) (Nat.of_int 3))` should evaluate to `6`; and `Nat.to_int (NatOps.plus (Nat.of_int 1) Nat.zero)` should evaluate to `1`.

To get full credit for this section, you must past at least 10/21 of the test cases.
